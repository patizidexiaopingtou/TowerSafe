# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../base/config.gni")
import("ftrace.gni")

ohos_source_set("ftrace_plugin_source") {
  sources = [
    "src/file_utils.cpp",
    "src/flow_controller.cpp",
    "src/ftrace_data_reader.cpp",
    "src/ftrace_field_parser.cpp",
    "src/ftrace_fs_ops.cpp",
    "src/ftrace_module.cpp",
    "src/ftrace_parser.cpp",
    "src/hitrace_ops.cpp",
    "src/kernel_symbols_parser.cpp",
    "src/paged_mem_pool.cpp",
    "src/printk_formats_parser.cpp",
    "src/process_utils.cpp",
    "src/result_transporter.cpp",
    "src/string_utils.cpp",
    "src/sub_event_parser.cpp",
    "src/trace_ops.cpp",
  ]
  defines = [ "_GNU_SOURCE" ]
  include_dirs = [
    "include",
    "src",
    "../../base/include",
    "../api/include",
    "${OHOS_PROFILER_DIR}/interfaces/kits",
    "${OHOS_PROFILER_DIR}/device/base/include",
    "//third_party/bounds_checking_function/include",
    "//commonlibrary/c_utils/base/include",
  ]
  deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
    "${OHOS_PROFILER_DIR}/protos/types/plugins/ftrace_data/${device_kernel_version}:ftrace_data_cpp",
    "tools/device_kernel_version/${device_kernel_version}/event_parsers:ftrace_event_parsers",
    "//third_party/bounds_checking_function:libsec_static",
  ]
  if (current_toolchain != host_toolchain) {
    defines += [ "HAVE_HILOG" ]
    if (build_l2) {
      external_deps = [ "shared_library:libhilog" ]
    } else {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
  }
  public_configs = [ "${OHOS_PROFILER_DIR}/device/base:hiprofiler_test_config" ]
}

ohos_shared_library("ftrace_plugin") {
  deps = [ ":ftrace_plugin_source" ]
  install_enable = true
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

group("ftrace_targets") {
  deps = [
    ":ftrace_plugin",
    "test:busy_test",
    "test:get_tzinfo",
  ]
}
