# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import(
    "../../../../../../../arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("../../../../filemanagement_aafwk.gni")

# compile .js to .abc.
action("gen_picker_abc") {
  visibility = [ ":*" ]
  script = "../../../../../../../arkcompiler/ets_frontend/ts2panda/scripts/generate_js_bytecode.py"

  args = [
    "--src-js",
    rebase_path("picker.js"),
    "--dst-file",
    rebase_path(target_out_dir + "/picker.abc"),
    "--node",
    rebase_path("${node_path}"),
    "--frontend-tool-path",
    rebase_path("${ts2abc_build_path}"),
    "--node-modules",
    rebase_path("${node_modules}"),
    "--module",
  ]
  deps = [
    "../../../../../../../arkcompiler/ets_frontend/ts2panda:ark_ts2abc_build",
  ]
  inputs = [ "picker.js" ]
  outputs = [ target_out_dir + "/picker.abc" ]
}

base_output_path = get_label_info(":picker_js", "target_out_dir")
picker_obj_path = base_output_path + "/picker.o"
gen_js_obj("picker_js") {
  input = "picker.js"
  output = picker_obj_path
  dep = ":gen_picker_abc"
}

abc_output_path = get_label_info(":picker_abc", "target_out_dir")
picker_abc_obj_path = abc_output_path + "/picker_abc.o"
gen_js_obj("picker_abc") {
  input = "$target_out_dir/picker.abc"
  output = picker_abc_obj_path
  dep = ":gen_picker_abc"
}

ohos_shared_library("picker") {
  ldflags = [ "-Wl" ]
  sources = [ "native_module_ohos_picker.cpp" ]

  deps = [
    ":picker_abc",
    ":picker_js",
    "${ability_runtime_path}/frameworks/native/ability/native:abilitykit_native",
  ]

  sanitize = {
    cfi = true
    debug = false
  }

  relative_install_dir = "module/file"
  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}
