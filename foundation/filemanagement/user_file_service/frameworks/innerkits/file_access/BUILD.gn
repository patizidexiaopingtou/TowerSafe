# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/filemanagement/user_file_service/filemanagement_aafwk.gni")

BASE_DIR = "//foundation/filemanagement/user_file_service"

config("ability_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "${BASE_DIR}/utils",
    "${ability_runtime_services_path}/common/include",
    "${ability_runtime_kits_path}/ability/native/include/continuation/distributed",
    "${ability_runtime_kits_path}/ability/native/include/continuation/kits",
    "${ability_runtime_kits_path}/appkit/native/app/include",
    "${ability_runtime_napi_path}/inner/napi_common",
    "//foundation/aafwk/standard/frameworks/kits/appkit/native/app/include",
    "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_base/include",
    "//foundation/bundlemanager/bundle_framework/interfaces/inner_api/appexecfwk_base/include",
    "//foundation/distributeddatamgr/distributedfile/interfaces/kits/js/src/common",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

config("ability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "include/notify",
    "${BASE_DIR}/utils",
    "${ability_runtime_kits_path}/appkit/native/ability_runtime/app",
    "${ability_runtime_kits_path}/appkit/native/app/include",
    "${ability_runtime_kits_path}/appkit/native/ability_runtime/context",
  ]
}

ohos_shared_library("file_access_extension_ability_kit") {
  include_dirs = [
    "${ability_runtime_path}/interfaces/kits/native/appkit/ability_runtime/context",
    "//foundation/communication/ipc/ipc/native/src/napi/include",
  ]

  sources = [
    "src/file_access_ext_ability.cpp",
    "src/file_access_ext_connection.cpp",
    "src/file_access_ext_proxy.cpp",
    "src/file_access_ext_stub.cpp",
    "src/file_access_ext_stub_impl.cpp",
    "src/file_access_helper.cpp",
    "src/js_file_access_ext_ability.cpp",
    "src/napi_common_fileaccess.cpp",
    "src/notify/file_access_notify_agent.cpp",
    "src/notify/file_access_notify_manager.cpp",
    "src/notify/file_access_notify_proxy.cpp",
    "src/notify/file_access_notify_stub.cpp",
  ]
  configs = [ ":ability_config" ]
  public_configs = [
    ":ability_public_config",
    "${ability_runtime_path}/frameworks/native/ability:ability_context_public_config",
  ]

  deps = [
    "${ability_runtime_path}/frameworks/native/ability/native:abilitykit_native",
    "${ability_runtime_path}/frameworks/native/appkit:app_context",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_manager",
    "ability_runtime:runtime",
    "access_token:libaccesstoken_sdk",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "hitrace_native:hitrace_meter",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  public_deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/arkui/napi:ace_napi",
  ]

  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}

ohos_shared_library("file_access_extension_ability_module") {
  sources = [ "src/file_access_ext_ability_module_loader.cpp" ]

  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  deps = [
    ":file_access_extension_ability_kit",
    "${ability_runtime_path}/frameworks/native/ability/native:abilitykit_native",
  ]

  external_deps = [
    "ability_runtime:runtime",
    "c_utils:utils",
  ]

  relative_install_dir = "extensionability/"
  subsystem_name = "filemanagement"
  part_name = "user_file_service"
}
