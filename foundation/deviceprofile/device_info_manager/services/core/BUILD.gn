# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos_var.gni")

device_profile_path = "//foundation/deviceprofile/device_info_manager"
device_profile_innerkits = "${device_profile_path}/interfaces/innerkits"
services = "${device_profile_path}/services"
device_profile_common_sources =
    [ "${device_profile_path}/common/src/device_profile_utils.cpp" ]

config("device_info_manager_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "include/authority",
    "include/devicemanager",
    "include/contentsensor",
    "include/dbstorage",
    "include/dfx",
    "include/subscribemanager",
    "${device_profile_path}/common/include",
    "${device_profile_innerkits}/core/include",
    "${services}/core/include",
    "//third_party/json/include",
  ]
}

ohos_shared_library("distributed_device_profile") {
  install_enable = true
  sources = [
    "src/authority/authority_manager.cpp",
    "src/authority/trust_group_manager.cpp",
    "src/contentsensor/app_info_collector.cpp",
    "src/contentsensor/content_collector.cpp",
    "src/contentsensor/content_sensor_manager.cpp",
    "src/contentsensor/device_info_collector.cpp",
    "src/contentsensor/storage_info_collector.cpp",
    "src/contentsensor/syscap_info_collector.cpp",
    "src/contentsensor/system_info_collector.cpp",
    "src/dbstorage/device_profile_storage.cpp",
    "src/dbstorage/device_profile_storage_manager.cpp",
    "src/dbstorage/kvstore_death_recipient.cpp",
    "src/dbstorage/online_sync_table.cpp",
    "src/dbstorage/sync_coordinator.cpp",
    "src/devicemanager/device_info.cpp",
    "src/devicemanager/dp_device_manager.cpp",
    "src/dfx/device_profile_dumper.cpp",
    "src/distributed_device_profile_service.cpp",
    "src/distributed_device_profile_stub.cpp",
    "src/profile_change_notification.cpp",
    "src/service_characteristic_profile.cpp",
    "src/subscribemanager/profile_change_handler.cpp",
    "src/subscribemanager/profile_event_handler.cpp",
    "src/subscribemanager/profile_event_handler_factory.cpp",
    "src/subscribemanager/profile_event_notifier_proxy.cpp",
    "src/subscribemanager/profile_sync_handler.cpp",
    "src/subscribemanager/subscribe_info.cpp",
    "src/subscribemanager/subscribe_info_checker.cpp",
    "src/subscribemanager/subscribe_manager.cpp",
    "src/subscribemanager/subscriber_death_recipient.cpp",
    "src/sync_options.cpp",
  ]
  sources += device_profile_common_sources

  configs = [
    ":device_info_manager_config",
    "//foundation/deviceprofile/device_info_manager/test/resource:coverage_flags",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "device_auth:deviceauth_sdk",
    "device_manager:devicemanagersdk",
    "dmsfwk:common_sdk",
    "eventhandler:libeventhandler",
    "hisysevent_native:libhisysevent",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "kv_store:distributeddata_inner",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "syscap_codec:syscap_interface_shared",
  ]

  part_name = "device_info_manager"
  subsystem_name = "deviceprofile"
}

group("unittest") {
  testonly = true
  deps = [ "test:unittest" ]
}
