# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

# compile .ts to .js.
action("build_ts_js") {
  script = "//commonlibrary/ets_utils/js_api_module/build_ts_js.py"

  args = [
    "--dst-file",
    rebase_path(target_out_dir + "/js_buffer.js"),
    "--module-path",
    rebase_path("/commonlibrary/ets_utils/js_api_module/buffer"),
    "--out-file",
    rebase_path("./out/js_buffer.js"),
    "--relative-path",
    rebase_path("//", root_build_dir),
  ]
  outputs = [ target_out_dir + "/js_buffer.js" ]
}

base_output_path = get_label_info(":js_buffer", "target_out_dir")
js_buffer_obj_path = base_output_path + "/buffer.o"
gen_js_obj("js_buffer") {
  input = "$target_out_dir/js_buffer.js"
  output = js_buffer_obj_path
  dep = ":build_ts_js"
}

# compile .js to .abc.
action("gen_buffer_abc") {
  visibility = [ ":*" ]
  script = "//arkcompiler/ets_frontend/ts2panda/scripts/generate_js_bytecode.py"

  args = [
    "--src-js",
    rebase_path(target_out_dir + "/js_buffer.js"),
    "--dst-file",
    rebase_path(target_out_dir + "/buffer.abc"),
    "--node",
    rebase_path("${node_path}"),
    "--frontend-tool-path",
    rebase_path("${ts2abc_build_path}"),
    "--node-modules",
    rebase_path("${node_modules}"),
    "--module",
  ]
  deps = [
    ":build_ts_js",
    "//arkcompiler/ets_frontend/ts2panda:ark_ts2abc_build",
  ]

  inputs = [ target_out_dir + "/js_buffer.js" ]
  outputs = [ target_out_dir + "/buffer.abc" ]
}

abc_output_path = get_label_info(":buffer_abc", "target_out_dir")
buffer_abc_obj_path = abc_output_path + "/buffer_abc.o"
gen_js_obj("buffer_abc") {
  input = "$target_out_dir/buffer.abc"
  output = buffer_abc_obj_path
  dep = ":gen_buffer_abc"
}

ohos_shared_library("buffer") {
  include_dirs = [
    "//third_party/icu/icu4c/source/common",
    "//third_party/node/src",
    "//foundation/arkui/napi/interfaces/kits",
    "//commonlibrary/ets_utils/js_api_module/buffer",
  ]

  sources = [
    "converter.cpp",
    "js_blob.cpp",
    "js_buffer.cpp",
    "native_module_buffer.cpp",
  ]

  external_deps = [ "c_utils:utils" ]
  deps = [
    ":buffer_abc",
    ":js_buffer",
    "//commonlibrary/ets_utils/js_api_module/buffer/:js_buffer",
    "//foundation/arkui/napi/:ace_napi",
    "//third_party/icu/icu4c:shared_icuuc",
  ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }
  subsystem_name = "commonlibrary"
  part_name = "ets_utils"

  relative_install_dir = "module"
}

ohos_static_library("buffer_static") {
  include_dirs = [
    "//third_party/icu/icu4c/source/common",
    "//third_party/node/src",
    "//foundation/arkui/napi/interfaces/kits",
    "//commonlibrary/ets_utils/js_api_module/buffer",
  ]

  sources = [
    "converter.cpp",
    "js_blob.cpp",
    "js_buffer.cpp",
    "native_module_buffer.cpp",
  ]

  external_deps = [ "c_utils:utils" ]
  deps = [
    ":buffer_abc",
    ":js_buffer",
    "//commonlibrary/ets_utils/js_api_module/buffer/:js_buffer",
    "//foundation/arkui/napi/:ace_napi",
    "//third_party/icu/icu4c:shared_icuuc",
  ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }
  subsystem_name = "commonlibrary"
  part_name = "ets_utils"
}

group("buffer_packages") {
  deps = [ ":buffer" ]
}
