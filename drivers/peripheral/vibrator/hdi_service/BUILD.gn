# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//drivers/hdf_core/adapter/uhdf2/uhdf.gni")
if (defined(ohos_lite)) {
  group("hdf_vibrator_service") {
    deps = []
    public_configs = []
  }
} else {
  ohos_shared_library("libvibrator_interface_service_1.1") {
    include_dirs = [
      "//drivers/hdf_core/adapter/uhdf2/include/hdi",
      "//drivers/peripheral/vibrator/interfaces/include/",
    ]
    sources = [ "vibrator_interface_impl.cpp" ]
    deps = [ "//drivers/peripheral/vibrator/hal:hdi_vibrator" ]

    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]

    if (is_standard_system) {
      external_deps = [
        "c_utils:utils",
        "drivers_interface_vibrator:vibrator_idl_headers",
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_single",
      ]
    } else {
      external_deps = [
        "hilog:libhilog",
        "ipc:ipc_single",
      ]
    }

    install_images = [ chipset_base_dir ]
    subsystem_name = "hdf"
    part_name = "drivers_peripheral_vibrator"
  }

  ohos_shared_library("libvibrator_driver") {
    include_dirs = [
      "//drivers/hdf_core/adapter/uhdf2/ipc/include",
      "//drivers/hdf_core/adapter/uhdf2/include/host",
      "//drivers/hdf_core/adapter/uhdf2/include/hdi",
      "//drivers/peripheral/vibrator/interfaces/include/",
    ]
    sources = [ "vibrator_interface_driver.cpp" ]

    deps = [
      "//drivers/hdf_core/adapter/uhdf2/ipc:libhdf_ipc_adapter",
      "//drivers/interface/vibrator/v1_1:libvibrator_stub_1.1",
    ]

    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]

    if (is_standard_system) {
      external_deps = [
        "c_utils:utils",
        "hdf_core:libhdi",
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_single",
      ]
    } else {
      external_deps = [
        "hilog:libhilog",
        "ipc:ipc_single",
      ]
    }

    install_images = [ chipset_base_dir ]
    subsystem_name = "hdf"
    part_name = "drivers_peripheral_vibrator"
  }

  group("hdf_vibrator_service") {
    deps = [
      ":libvibrator_driver",
      ":libvibrator_interface_service_1.1",
    ]
  }
}
