# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//drivers/hdf_core/adapter/uhdf2/uhdf.gni")
import("//drivers/peripheral/audio/audio.gni")

group("hdi_audio") {
  deps = []
  if (drivers_peripheral_audio_user_mode == false) {
    if (drivers_peripheral_audio_rich_device == false) {
      deps += [ "hal/hdi_passthrough:hdi_audio" ]
    }
  } else {
    deps += [ "//device/soc/${product_company}/common/hal/media:hdi_audio" ]
  }

  if (defined(ohos_lite)) {
    deps += [ "config/lite:hdf_audio_adapter_json" ]
  } else {
    if (drivers_peripheral_audio_hdf_proxy_stub == true) {
      deps += [
        "config/linux:hdf_audio_adapter_json",
        "hal/hdi_binder/proxy:hdi_audio_client",
        "hal/hdi_binder/server:hdi_audio_a2dp_server",
        "hal/hdi_binder/server:hdi_audio_pnp_server",
        "hal/hdi_binder/server:hdi_audio_primary_server",
        "hal/hdi_binder/server:hdi_audio_usb_server",
      ]
    }
    deps += [ "hdi_service:idl_audio" ]

    if (drivers_peripheral_audio_alsa_lib) {
      deps += [ "config/linux:hdf_alsa_adapter_json" ]
    }
  }

  if (drivers_peripheral_audio_user_mode == false) {
    deps += [
      "supportlibs:hdi_audio_interface_lib_capture",
      "supportlibs:hdi_audio_interface_lib_render",
    ]
  }

  if (drivers_peripheral_audio_hal_notsupport_pathselect == false) {
    deps += [ "hal/pathselect:hdi_audio_path_select" ]

    if (defined(ohos_lite)) {
      deps += [ "config/lite:hdf_audio_path_json" ]
    } else {
      deps += [ "config/linux:hdf_audio_path_json" ]
    }
  }

  if (drivers_peripheral_audio_policy_config == true) {
    deps += [
      "//vendor/${product_company}/${product_name}/audio:audio_policy_config",
    ]
  }
}
